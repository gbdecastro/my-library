buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "com.diffplug.spotless:spotless-plugin-gradle:5.1.0"
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'org.liquibase.gradle' version '2.2.0'
	id 'jacoco'
}

apply plugin: "jacoco"
apply from: "${projectDir}/gradle/liquibase.gradle"
apply from: "${projectDir}/gradle/spotless.gradle"

group = 'com.gbdecastro'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	// Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.1.5'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// MySQL
	implementation 'mysql:mysql-connector-java:8.0.30'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Map Struct
	compileOnly group: 'org.mapstruct', name: 'mapstruct', version: '1.3.1.Final'
	annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.3.1.Final'
	testAnnotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.3.1.Final'

	// Liquibase
	implementation 'org.liquibase:liquibase-core:4.17.2'
	liquibaseRuntime 'info.picocli:picocli:4.6.3'
	liquibaseRuntime 'org.liquibase:liquibase-core:4.17.2'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
	liquibaseRuntime 'mysql:mysql-connector-java:8.0.30'

	//Unit Tests & Integration Tests
	runtimeOnly 'com.h2database:h2'
	testCompileOnly group: 'org.mapstruct', name: 'mapstruct', version: '1.3.1.Final'
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompileOnly 'org.junit.jupiter:junit-jupiter-params:5.9.0'

	//Open api
	implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.13'
}

test {
	useJUnitPlatform()
	testLogging {
		exceptionFormat = 'full'
		events = ["passed", "failed", "skipped"]
	}
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
	group = "build"
	reports {
		html.enabled true
		xml.enabled false
		csv.enabled false
		html.destination file("${projectDir}/jacoco")
	}
}